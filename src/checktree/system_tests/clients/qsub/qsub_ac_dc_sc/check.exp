#___INFO__MARK_BEGIN__
##########################################################################
#
#  The Contents of this file are made available subject to the terms of
#  the Sun Industry Standards Source License Version 1.2
#
#  Sun Microsystems Inc., March, 2001
#
#
#  Sun Industry Standards Source License Version 1.2
#  =================================================
#  The contents of this file are subject to the Sun Industry Standards
#  Source License Version 1.2 (the "License"); You may not use this file
#  except in compliance with the License. You may obtain a copy of the
#  License at http://gridengine.sunsource.net/Gridengine_SISSL_license.html
#
#  Software provided under this License is provided on an "AS IS" basis,
#  WITHOUT WARRANTY OF ANY KIND, EITHER EXPRESSED OR IMPLIED, INCLUDING,
#  WITHOUT LIMITATION, WARRANTIES THAT THE SOFTWARE IS FREE OF DEFECTS,
#  MERCHANTABLE, FIT FOR A PARTICULAR PURPOSE, OR NON-INFRINGING.
#  See the License for the specific provisions governing your rights and
#  obligations concerning the Software.
#
#  The Initial Developer of the Original Code is: Sun Microsystems, Inc.
#
#  Copyright: 2001 by Sun Microsystems, Inc.
#
#  All Rights Reserved.
#
#  Portions of this software are Copyright (c) 2011 Univa Corporation.
#
#  Portions of this software are Copyright (c) 2024 HPC-Gridware GmbH
#
##########################################################################
#___INFO__MARK_END__

# Define the global variables to give them back
global check_name check_description check_needs check_functions
global check_category
global ts_config
global check_root_access_needs
set check_root_access_needs "yes"

# set description for each level
set check_description(0)  "check every qsub option"

set check_name         "qsub_ac_dc_sc"
set check_category     "COMPATIBILITY SYSTEM QSUB VERIFIED"
set check_needs        "init_core_system"       ;# dependencies of this check (name of other check)

# setup and cleanup functions
set check_setup_function "qsub_setup"
set check_cleanup_function "qsub_cleanup"

# define test's procedure order
set check_functions {}
lappend check_functions "check_option_ac"
lappend check_functions "check_option_dc"
lappend check_functions "check_option_sc"

proc qsub_setup {} {
   global ts_config
   global CHECK_JOB_OUTPUT_DIR
   global stored_configuration
   global job_output_dir test_host default_args default_q_args default_h_args
   global steps_per_queue size_interval

   # set some global variables
   set num_queues [llength $ts_config(unique_arch_nodes)] 
   set job_output_dir $CHECK_JOB_OUTPUT_DIR

   # run host specific tests on a random host
   set test_host [host_conf_get_suited_hosts]

   # set default jobargs
   set default_args "-o $job_output_dir -e $job_output_dir $ts_config(product_root)/examples/jobs/sleeper.sh 5"
   set qinstance [get_queue_instance "all.q" $test_host]
   set default_q_args "-q $qinstance $default_args"
   set default_h_args "-l h=${test_host} $default_args"

   # backup global config
   get_config stored_configuration
  
   # set config for fast scheduling / fast writing of accounting record
   set my_config(reporting_params) "accounting=true reporting=false flush_time=00:00:01 joblog=true sharelog=00:10:00"
   set_config my_config
}

proc qsub_cleanup {} {
   global ts_config
   global stored_configuration
   global job_output_dir test_host default_args default_q_args default_h_args
   global steps_per_queue size_interval

   set spool_dir [get_spool_dir $ts_config(master_host)  "qmaster"] 
   if {[is_qmaster_alive $ts_config(master_host) $spool_dir] != 1} {
      if {[is_scheduler_alive $ts_config(master_host) $spool_dir] != 1} {
         startup_qmaster 1
      } else {
      startup_qmaster 0
      }
   }

   delete_all_jobs
   wait_for_end_of_all_jobs

   reset_config stored_configuration

   # unset our test global variables
   foreach var "stored_configuration job_output_dir test_host default_args default_q_args default_h_args steps_per_queue size_interval" {
      if {[info exists $var]} {
         unset $var
      }
   }
}

proc qsub_submit_check {jobargs expect_submit_error {user ""} {host ""} {output_files 2}} {
   global ts_config CHECK_FIRST_FOREIGN_SYSTEM_USER
   global job_output_dir

   # default user is the first "foreign" user!
   if {$user == ""} {
      set user $CHECK_FIRST_FOREIGN_SYSTEM_USER
   }

   # default submit host is "any host"
   if {$host == ""} {
      set host [host_conf_get_suited_hosts]
   }

   # if we expect a submit error, submit_job shall not do error checking
   set submit_error_check 1
   if {$expect_submit_error} {
      set submit_error_check 0
   }

   # submit job and evaluate result
   set job_id [submit_job $jobargs $submit_error_check 60 $host $user]
   if {$expect_submit_error} {
      if {$job_id > 0} {
         ts_log_severe "submission of job \"$jobargs\" as user $user on host \"$host\" should have failed"
         delete_job $job_id
      }
      # are done
      return 0
   } else {
      if {$job_id < 0} {
         ts_log_severe "submission of job \"$jobargs\" as user $user on host \"$host\" failed:\nerror $job_id"
         return 0
      }
   }

   # wait for job accounting record and expect 1 or 2 ($output_files) output/error files
   set time [expr [timestamp] + 300] 
   while {[timestamp] < $time}  {
      after 1000
      set back [was_job_running $job_id 0]
      if {$back != -1} {
         set back [del_job_files $job_id $job_output_dir $output_files]
         if {$back != $output_files} {
            ts_log_severe "job $job_id (\"$jobargs\") did not produce error and output file"
            return 0
         } else {
            # job ran through successfully
            return $job_id
         }
      }
   }

   # if we get here, we didn't find an accounting record within reasonable time
   ts_log_severe "timeout waiting for job $job_id (\"$jobargs\")"

   # cleanup
   delete_job $job_id
   del_job_files $job_id $job_output_dir 2
   return 0
}
 
proc check_option_ac {} {
   global ts_config CHECK_ACTUAL_TEST_PATH
   global job_output_dir test_host

   # first clean up output directory:
   setup_output_directory

   set option "-ac myvar1=\"test 1\" -ac myvar2='test 2'"
   # We use a special job script which contains context variable settings
   set jobargs "$option -l h=${test_host} -o $job_output_dir -e $job_output_dir $CHECK_ACTUAL_TEST_PATH/sleeper.sh 60"
   set result [submit_job $jobargs 0]

   if {$result < 0} {
      ts_log_severe "error submitting job \"$jobargs\"\nerror $result"
      return
   }

   set qstatoutput [start_sge_bin "qstat" "-j $result"]

   if {$prg_exit_state != 0} {
      ts_log_severe "error executing qstat -j $result"
      return
   }

   if {[regexp {context:([^\r\n]+\r?\n)} $qstatoutput match vars] != 1} {
      ts_log_severe "qsub $option - qsub error: context variables were not set"
      return
   }

   ts_log_fine "context variables: $vars"

   if {[regexp {[ \t,]myvar1=test 1[,\r\n]} $vars match] != 1} {
      ts_log_severe "qsub $option - qsub error: command-line context variable was not set"
      return
   } elseif {[regexp {[ \t,]myvar2=test 2[,\r\n]} $vars match] != 1} {
      ts_log_severe "qsub $option - qsub error: command-line context variable was not set"
      return
   } elseif {[regexp {[ \t,]myvar3=test 3[,\r\n]} $vars match] != 1} {
      ts_log_severe "qsub $option - qsub error: inline context variable was not set"
      return
   } elseif {[regexp {[ \t,]myvar4=test 4[,\r\n]} $vars match] != 1} {
      if {[regexp {[ \t,]myvar4='test 4'[,\r\n]} $vars match] == 1} {
         ts_log_info "qsub $option - qsub error: issue IZ1850 encountered only fixed for versions > 6.0"
         return
      }

      ts_log_severe "qsub $option - qsub error: inline context variable was not set"
      return
   }

   set time [expr [timestamp] + 300]
   while {[timestamp] < $time}  {
      after 1000
      set back [was_job_running $result 0]
      if {$back != -1} {
         set back [del_job_files $result $job_output_dir 2]
         if {$back != 2} {
            ts_log_severe "job $result did not produce error and output file\njobargs: $jobargs"
         }
         return
      }
   }
 
   ts_log_severe "timeout while waiting for job accounting"
}

proc check_option_dc {} {
   global ts_config
   global default_args

   # first clean up output directory:
   setup_output_directory

   set option "-dc myvar,myvar2"
   set jobargs "$option $default_args"
   qsub_submit_check $jobargs 0
}

proc check_option_sc {} {
   global ts_config
   global default_args

   # first clean up output directory:
   setup_output_directory

   set option "-sc myvar=test3"
   set jobargs "$option $default_args"
   qsub_submit_check $jobargs 0
}
