#___INFO__MARK_BEGIN_NEW__
###########################################################################
#
#  The contents of this file are made available subject to the terms of the
#  Apache Software License 2.0 ('The License').
#  You may not use this file except in compliance with The License.
#  You may obtain a copy of The License at
#  http://www.apache.org/licenses/LICENSE-2.0.html
#
#  Copyright (c) 2011 Univa Corporation.
#
#  Portions of this software are Copyright (c) 2024 HPC-Gridware GmbH
#
###########################################################################
#___INFO__MARK_END_NEW__

global check_root_access_needs

set check_root_access_needs "yes"

# define test's name and run level descriptions
set check_name            "home_dirs"
set check_category        "COMPATIBILITY SYSTEM VERIFIED"
set check_description(0)  "checks if the users home directories are shared"


# define test's dependencies
set check_needs           "connection_test"

# define test's procedure order
set check_functions "home_dirs_test"

#****** home_dirs_test() *******************************************************
#  NAME
#     home_dirs_test() -- tests if the home dirs are shared
#
#  SYNOPSIS
#     home_dirs_test {}
#
#  FUNCTION
#     Tests if the home directories of all testsuite users are shared among
#     all test hosts. Several tests rely on this being set up correctly.
#
#*******************************************************************************
proc home_dirs_test {} {
   global CHECK_USER CHECK_FIRST_FOREIGN_SYSTEM_USER CHECK_SECOND_FOREIGN_SYSTEM_USER
   global ts_config

   # get all test hosts (doesn't matter if some occur twice)
   set hostlist      $ts_config(execd_hosts)
   lappend hostlist  $ts_config(master_host)
   if {[string compare $ts_config(shadowd_hosts) "none"] != 0} {
      foreach host $ts_config(shadowd_hosts) {
         lappend hostlist $host
      }
   }
   if {[string compare $ts_config(admin_only_hosts) "none"] != 0} {
      foreach host $ts_config(admin_only_hosts) {
         lappend hostlist $host
      }
   }
   if {[string compare $ts_config(submit_only_hosts) "none"] != 0} {
      foreach host $ts_config(submit_only_hosts) {
         lappend hostlist $host
      }
   }

   # make the host list unique
   set hostlist [lsort -unique $hostlist]

   # get one host where the test file will be written
   set write_host [host_conf_get_suited_hosts 1]

   set test_file "\$HOME/testfile123"

   # loop over all testsuite users and check their home dirs
   foreach user "$CHECK_USER $CHECK_FIRST_FOREIGN_SYSTEM_USER $CHECK_SECOND_FOREIGN_SYSTEM_USER" {
      # write the test file to the users home directory
      ts_log_fine "As user $user write file '$test_file' on host $write_host"
      set result [start_remote_prog $write_host $user "touch" $test_file]
      if {$result != ""} {
         ts_log_severe "Touching '$test_file' in user $user's home directory \
                        failed. Output of touch: $result"
         return
      }

      # check on all hosts if the file appears
      foreach host $hostlist {
         set result [wait_for_remote_file $host $user $test_file]
         if {$result != 0} {
            # error was already raised, just return
            return
         }
      }

      # delete the file from the users home directory
      set result [start_remote_prog $write_host $user "rm" $test_file]
      if {$result != ""} {
         ts_log_severe "Deleting '$test_file' in user $user's home directory \
                        failed. Output of rm: $result"
         return
      }
      ts_log_fine "Successfully deleted file '$test_file' in user $user's home directory."
   }
}

